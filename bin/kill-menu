#!/usr/bin/env bb

;; ---
;; title:  kill processes menu
;; author: emil lenz
;; email:  emillenz@protonmail.com
;; date:   [2024-12-06] <- (initial ruby version [2023-05-08])
;; info:
;; - intended for use with a WM.
;; - any provided arguments are directly passed on to dmenu (use dmenu's flags to customize the prompt/theme)
;; dependencies:
;; - dunst
;; ---

(require '[babashka.process :refer [shell process check]])
(require '[clojure.string :as str])

(defn names=>ids []
  (->> (shell {:out :string}
              "ps" "-e" "-o" "pid,comm")
       :out
       str/split-lines
       rest
       (map (fn [line]
              (let  [[padding id name] (str/split line #"\s+")]
                [name id])))
       (into {})))

(defn select-ps [names=>ids ]
  (->> (apply process
              {:in (->> names=>ids
                        keys
                        (map name)
                        (str/join "\n"))
               :out :string}
              "dmenu"
              "-p" "killprocess >"
              *command-line-args*)
       check
       :out
       str/trim))

(defn main []
  (let [names=>ids (names=>ids)
        selected-ps (select-ps names=>ids)]

    (shell "kill" (get names=>ids selected-ps))
    (shell "dunstify" "killed process" selected-ps)))

(main)
