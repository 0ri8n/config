#!/usr/bin/env bb

;; ---
;; title:  bluetooth device menu
;; author: emil lenz
;; email:  emillenz@protonmail.com
;; date:   [2024-12-07] <- (initial ruby version: [2024-05-26]
;; info:
;; - intended for use with a WM.
;; - any provided arguments are directly passed on to dmenu (use dmenu's flags to customize the prompt/theme)
;; dependecies:
;; - dunst
;; - bluetoothctl
;; - dmenu
;; ---

(require '[babashka.process :refer [shell process check]])
(require '[clojure.string :as str])

(defn addresses=>devices []
  (->> (shell {:out :string} "bluetoothctl devices")
       check
       :out
       str/split-lines
       (map (fn [line]
              (let [[dev addr name]
                    (str/split line #"\s" 3)]
                [name addr])))
       (into {})))

(def off-cmd "/off")

(defn selected-device [addresses=>devices]
  (->> (apply shell
              {:in (->> (conj (keys addresses=>devices) off-cmd)
                        (str/join "\n"))
               :out :string}
              "dmenu"
              "-p" "bluetooth-device >"
              *command-line-args*)
       check
       :out
       str/trim))

(defn connect [addresses=>devices selected-device]
  (shell "bluetoothctl" "power" "on")
  (try (do
         (shell "dunstify" "bluetooth: connecting" selected-device)
         (->> (shell "bluetoothctl" "connect" (get addresses=>devices selected-device))
              check)
         (shell "dunstify" "bluetooth: connected" selected-device))
       (catch Exception e
         (shell "dunstify" "bluetooth" (format "[error] failed to connect to device: %s" selected-device)))))

(defn off []
  (->> (shell "bluetoothctl" "power" "off")
       check)
  (shell "dunstify" "bluetooth: off"))

(defn main []
  (let [addresses=>devices (addresses=>devices)
        selected-device (selected-device addresses=>devices)]

    (if (= selected-device off-cmd)
      (off)
      (connect addresses=>devices selected-device))))

(main)
